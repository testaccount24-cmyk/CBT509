/* REXX

Copyright (C) 2001 Tim Kent Larsen

This material is free software and is distributed as-is WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.

Permission is granted to make any number of machine-readable copies of
this material, redistribute it and/or modify it under the terms of the
GNU General Public License as published by the Free Software Foundation.
*/

ARG FRAME .

"cgiutils -status 200 -ct text/html -expires now"

LINKS = 'STYLE="TEXT-DECORATION:NONE"'
HLITE = 'COLOR=WHITE'
COLOR = 'BGCOLOR=BLACK TEXT=LAWNGREEN',
        'LINK=TURQUOISE ALINK=WHITE VLINK=TURQUOISE'

DO I = 1 TO __ENVIRONMENT.0
  PARSE VAR __ENVIRONMENT.I TOKEN '=' VALUE
  CALL  VALUE TOKEN,VALUE
END

SAY '<HTML>'
SAY '<HEAD>'
SAY '<TITLE>'HTTP_HOST''SCRIPT_NAME'</TITLE>'
SAY '</HEAD>'

IF FRAME = '' THEN
  DO
    SAY '<FRAMESET COLS="30%,*">'
    SAY '<FRAME    NAME=MENU SRC="'SCRIPT_NAME'?MENU">'
    SAY '<FRAME    NAME=DATA SRC="'SCRIPT_NAME'?DATA">'
    SAY '</FRAMESET>'
  END
ELSE
  DO
    SAY '<BODY' COLOR'>'
    SAY '<PRE>'

    CALL    BPXWDYN "ALLOC DD(SHOWMVS) UNIT(SYSVIO)"
    ADDRESS LINKMVS "SHOWMVS"
    ADDRESS MVS     "EXECIO * DISKR SHOWMVS (FINIS"
    CALL    BPXWDYN "FREE  DD(SHOWMVS)"

    PARSE PULL 2 TITLE
    PARSE PULL
    TITLE = SPACE(TITLE,1)

    IF FRAME = 'MENU' THEN
      BASE = 'DATA'
    ELSE
      BASE = 'MENU'

    DO QUEUED()

      PARSE PULL 2 DATA
      DATA = STRIP(DATA,'T')
      HEAD = SPACE(STRIP(DATA,'T',':'),1)

      IF LEFT(DATA,1) >= 'A' THEN
        SAY '<A' LINKS 'TARGET='BASE,
            'HREF="'SCRIPT_NAME'?'BASE'#'HEAD'"',
            'NAME="'HEAD'">'HEAD'</A>'
      ELSE
        IF FRAME = 'DATA' THEN SAY DATA
    END

    IF FRAME = 'DATA' THEN SAY '<FONT' HLITE'>'TITLE'</FONT>'

    SAY '</PRE>'
    SAY '</BODY>'
  END

SAY '</HTML>'
